prototype(Sfi.Site:DigestMail) < prototype(Psmb.Newsletter:MailRenderer) {
	@context.nodes = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('type', 'ourNews').queryFilter('bool', {must_not: [{term: {excludeFromNewsletter: true}}]}).greaterThan('firstPublicationDateTime', Date.format(Date.subtract(Date.now(), subscription.interval), "Y-m-d\TH:i:sP")).sortDesc('date').execute()}
	@if.notEmpty = ${q(nodes).count() > 0}
	subject = ${Date.format(Date.now(), 'd.m. ') + Translation.translate('newsletter.digestSubject', null, [], null, 'Sfi.Site')}
	body = Neos.Fusion:Value {
		@process.cssToInline = Psmb.Newsletter:CssToInline {
			cssPath = 'resource://Sfi.Site/Public/built/index.css'
		}
		value = Neos.Fusion:Array {

			headline = ${'<div class="alpha--light marginVertical-double">' + Translation.translate('newsletter.digestHeadline', null, [], null, 'Sfi.Site') + '</div>'}

			articles = Neos.Fusion:Collection {
				@process.tmpl = ${'<ul class="stream__content js-stream__content">' + value + '</ul>'}
				collection = ${nodes}
				itemName = 'node'
				itemRenderer = Sfi.Site:NewsShort {
					thumbUri.width = 600
					thumbUri.height = 400
					sectionName = 'normal'
					prototype(ImageUri) {
						width = 600
						height = 400
					}
					prototype(NodeUri) {
						absolute = ${true}
					}
				}
			}

			moreButton = Neos.Fusion:Tag {
				@process.tmpl = ${'<div class="paddingTop-double">' + value + '</div>'}
				tagName = 'a'
				attributes.class = 'button'
				attributes.href = NodeUri {
					node = ${site}
					absolute = ${true}
				}
				content = ${Translation.translate('newsletter.digestReadMore', null, [], null, 'Sfi.Site')}
			}

			@process.layout = Sfi.Site:MailLayout

			@cache {
				entryIdentifier {
					site = ${site}
				}
				mode = 'cached'
			}
		}
	}
}

prototype(Sfi.Site:DigestArticle) < prototype(Neos.Fusion:Tag) {
	tagName = 'a'
	attributes.href = NodeUri {
		node = ${node}
		absolute = ${true}
	}
	content = Neos.Fusion:Array {
		@process.tmpl = ${'<div class="marginVertical-double">' + value + '</div>'}
		title = ${'<h2 class="gamma link">' + String.stripTags(node.properties.title) + '</h2>'}
		teaser = ${'<p class="zeta">' + String.stripTags(node.properties.teaser) + '</p>'}
	}
}
