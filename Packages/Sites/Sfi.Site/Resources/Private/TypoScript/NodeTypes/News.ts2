prototype(Sfi.Site:News) {
	templatePath = 'resource://Sfi.Site/Private/TypoScript/NodeTypes/News.html'
	tagList = Sfi.Site:TagList
	cover = TYPO3.Neos:ContentCollection {
		nodePath = 'cover'
	}
	main = TYPO3.Neos:ContentCollection {
		nodePath = 'main'
	}
	newsList = Sfi.Site:NewsRelatedList
	disqus = Sfi.Site:Disqus
	stream = Sfi.Site:Stream
	collectionArticles = Sfi.Site:CollectionArticles
	share = Sfi.Site:Share

	audioList = Sfi.Site:AudioPlayer

	@process.contentElementWrapping = TYPO3.Neos:ContentElementWrapping
}

# Get thumb from news node
prototype(Sfi.Site:NewsThumb) < prototype(T:Value) {
	value = ${q(node).property('image') ? q(node).property('image') : q(node).children('cover').children('[instanceof Psmb.NodeTypes:Image]').first().property('image')}
}

prototype(Sfi.Site:NewsShortSmall) < prototype(T:Value) {
	value = T:Tag {
		@process.tmpl = ${'<li class="media-small"><div class="media-small__wrap">' + value + '</div></li>'}
		tagName = 'a'
		attributes.href = NodeUri {
			node = ${node}
		}
		content = T:Array {
			image = ImageTag {
				attributes.class = 'media-small__image media-small--left__image'
				asset = Sfi.Site:NewsThumb
				# No idea why this is needed
				maximumWidth>
				width = 60
				height = 60
				allowCropping = TRUE
			}
			body = ${'<div class="media-small__body"><h3 class="media-small__title">' + node.properties.title + '</h3></div>'}
		}
	}
}
