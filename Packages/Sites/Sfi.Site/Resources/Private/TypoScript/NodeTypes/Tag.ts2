prototype(Sfi.Site:AbstractTag) < prototype(T:Array) {
	title = ${'<div class="row"><div class="fixed-width"><div class="columns"><h1 class="page-title">' + node.properties.title + '</h1></div></div></div>'}
	newsList = Sfi.Site:Stream {
		@process.tmpl = ${'<div class="row bg-gray"><div class="fixed-width"><div class="columns">' + value + '</div></div></div>'}
	}
}

prototype(Sfi.Site:Tag) {
	templatePath = 'resource://Sfi.Site/Private/TypoScript/NodeTypes/Tag.html'
	title = ${q(node).parent().get(0) == q(site).children('tags').get(0) ? q(site).children('tags').get(0).properties.title : q(node).property('title')}
	cover = TYPO3.Neos:ContentCollection {
		nodePath = 'cover'
		prototype(Psmb.NodeTypes:Image) {
			content.image.maximumHeight = 600
		}
	}
	main = TYPO3.Neos:ContentCollection {
		nodePath = 'main'
	}
	stream = Sfi.Site:Stream {
		@process.tmpl = ${'<div class="row bg-gray"><div class="fixed-width"><div class="columns">' + value + '</div></div></div>'}
		@if.notRoot = ${q(node).parent().get(0) != q(site).children('tags').get(0)}
	}
	tagsMenu = T:Array {
		# TODO: refactor
		@if.isDescendantOfTags = ${q(node).parents().get(1) == q(site).children('tags').get(0) || q(node).parent().get(0) == q(site).children('tags').get(0)}
		tagsHeader = T:Collection {
			@process.tmpl = ${'<div class="stripe"><div class="fixed-width"><dl class="tabs" data-tab>' + value + '</dl></div></div>'}
			collection = ${q(site).children('tags').children('[instanceof TYPO3.Neos:Document]').get()}
			iterationName = 'iterator'
			itemRenderer = T:Tag {
				tagName = 'dd'
				attributes.class = ${q(documentNode).parent().get(0) == q(site).children('tags').get(0) ? (node == documentNode && 'active') : (iterator.isFirst && 'active')}
				content = T:Tag {
					tagName = 'a'
					attributes.href = ${'#' + node.name}
					content = ${node.properties.title}
				}
			}
		}
		tagsContent = T:Collection {
			@process.tmpl = ${'<div class="row"><div class="fixed-width"><div class="tabs-content columns">' + value + '</div></div></div>'}
			collection = ${q(site).children('tags').children().get()}
			iterationName = 'iterator'
			itemRenderer = T:Collection {
				@process.tmpl = ${'<div class="content ' + (q(documentNode).parent().get(0) == q(site).children('tags').get(0) ? (node == documentNode && 'active') : (iterator.isFirst && 'active')) +'" id="' + node.name + '"><ul>' + value + '</ul></div>'}
				collection = ${q(node).children('[instanceof Sfi.Site:Tag]').sort('navTitle', 'ASC').get()}
				itemRenderer = T:Tag {
					@process.tmpl = ${'<li class="epsilon">' + value + '</li>'}
					tagName = 'a'
					attributes.href = NodeUri {
						node = ${node}
					}
					content = ${node.properties.navTitle || node.properties.title}
				}
			}
		}
		@cache {
			mode = 'cached'
			entryIdentifier {
				node = ${node}
			}
			entryTags {
				1 = 'NodeType_Sfi.Site:Tag'
			}
		}
	}
}

prototype(Sfi.Site:PlaceTag)>
prototype(Sfi.Site:PlaceTag) < prototype(Sfi.Site:AbstractTag)
