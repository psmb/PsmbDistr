prototype(Sfi.Site:Story) {
	templatePath = 'resource://Sfi.Site/Private/TypoScript/NodeTypes/Story.html'
	@context.node = ${node.properties.collection ? node.properties.collection : node}

	imageUri = ImageUri {
		asset = ${node.properties.image}
	}

	title = ${node.properties.title}
	description = ${node.properties.description}

	feed = T:Collection {
		collection = ${Search.query(site).nodeType('Sfi.Site:TaggableMixin').exactMatch('collections', node.identifier).sortDesc('date').limit(5).execute()}
		itemRenderer = T:Case {
			isFirst {
				condition = ${(iterator.cycle == 1 && q(node).is('[instanceof Sfi.Site:News]')) ? true : false}
				renderer = Sfi.Site:StoryItemFirst
			}
			default {
				condition = ${true}
				renderer = Sfi.Site:StoryItem
			}
		}
		@cache {
			mode = 'cached'
			entryIdentifier {
				node = ${node}
			}
			entryTags {
				1 = ${'Node_' + node.identifier}
			}
		}
	}
	allLink = NodeUri {
		node = ${node}
	}
}

prototype(Sfi.Site:StoryItemFirst) < prototype(T:Tag) {
	@process.tmpl = ${'<li class="StoryFeed-item">' + value + '</li>'}
	tagName = 'a'
	attributes {
		@ignoreProperties = ${['imageUri']}
		imageUri = ImageUri {
			asset = Sfi.Site:NewsThumb
			maximumWidth = 640
			maximumHeight = 420
			allowCropping = ${true}
		}
		class = 'StoryItemFirst'
		style = ${'display: block; background-image: url(' + this.imageUri + ')'}
		href = NodeUri {
			node = ${node}
		}
	}
	title = ${node.properties.title}
	content = ${'
		<div class="StoryItemFirst-overlay">
			<div class="StoryItemFirst-inner">
				<div class="epsilon color-white"><strong>' + this.title + '</strong></div>
			</div>
		</div>
	'}
}
prototype(Sfi.Site:StoryItem) < prototype(T:Tag) {
	@process.tmpl = ${'<li class="StoryFeed-item">' + value + '<div class="StoryFeed-line"></div></li>'}
	tagName = 'a'
	attributes.class = 'epsilon color-white'
	attributes.href = NodeUri {
		node = ${node}
	}
	content = ${node.properties.title}
	content.@process.isBlog = ${q(node).is('[instanceof Sfi.Site:Blog]') ? 'Пишут в блогах: ' + node.properties.author.properties.name + ' &ndash; ' + value : value}
}
