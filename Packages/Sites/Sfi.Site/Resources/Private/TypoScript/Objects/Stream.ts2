prototype(Sfi.Site:StreamQuery) < prototype(T:Case) {
	announcements {
		condition = ${node.name == 'node-57c7aa010c560'}
		renderer = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('type', 'announcement').sortDesc('date')}
	}
	externalNews {
		condition = ${node.name == 'node-57cea530c2c46'}
		renderer = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('type', 'externalNews').sortDesc('date')}
	}
	ourNews {
		condition = ${true}
		renderer = T:Value {
			value = ${Search.query(site).nodeType('Sfi.Site:News')}
			value.@process.filterByRelation = ${site == node ? value : value.queryFilter('bool', {should: [
				{term: {sections: node.identifier}},
				{term: {tags: node.identifier}},
				{term: {places: node.identifier}},
				{term: {projects: node.identifier}},
				{term: {collections: node.identifier}}
			]})}
			value.@process.filterByType = T:Case {
				place {
					condition = ${request.arguments.place}
					renderer = ${value.exactMatch('places', request.arguments.place)}
				}
				collection {
					condition = ${request.arguments.collection}
					renderer = ${value.exactMatch('collections', request.arguments.collection)}
				}
				photo {
					condition = ${request.arguments.media == 'isPhoto'}
					renderer = ${value.exactMatch('isPhoto', true)}
				}
				audio {
					condition = ${request.arguments.media == 'isAudio'}
					renderer = ${value.exactMatch('isAudio', true)}
				}
				video {
					condition = ${request.arguments.media == 'isVideo'}
					renderer = ${value.exactMatch('isVideo', true)}
				}
				noType {
					condition = ${request.arguments.media == 'noType'}
					renderer = ${value.queryFilter('bool', {must_not: [
						{term: {type: 'ourNews'}},
						{term: {type: 'externalNews'}},
						{term: {type: 'announcement'}}
					]})}
				}
				defaultMain {
					condition = ${site == node}
					renderer = ${value.exactMatch('type', 'ourNews')}
				}
				default {
					condition = ${true}
					renderer = ${value}
				}
			}
		}
	}
}

prototype(Sfi.Site:StreamAggregations) < prototype(T:Value) {
	@context.request = 'ourNews'
	mediaConfig = T:RawArray {
		filters = T:RawArray {
			filters = T:RawArray {
				isPhoto = ${{term: {isPhoto: true}}}
				isAudio = ${{term: {isAudio: true}}}
				isVideo = ${{term: {isVideo: true}}}
			}
		}
	}
	collectionsConfig = T:RawArray {
		terms = T:RawArray {
			field = 'collections'
			size = 100
		}
	}
	placesConfig = T:RawArray {
		terms = T:RawArray {
			field = 'places'
			size = 100
		}
	}
	value = ${query.aggregation('media', this.mediaConfig).aggregation('collections', this.collectionsConfig)}
	value.@process.addPlaces = ${showPlaces ? value.aggregation('places', this.placesConfig) : value}
	value.@process.execute = ${value.execute().aggregations}
}

prototype(Sfi.Site:Stream) < prototype(T:Template) {
	templatePath = 'resource://Sfi.Site/Private/TypoScript/Objects/Stream.html'
	attributes = T:Attributes {
		class = "stream js-stream"
		id = "more_news"
	}
	i18n = T:RawArray {
		loadMore = ${Translation.translate('loadMore', null, [], null, 'Sfi.Site')}
		end = ${Translation.translate('end', null, [], null, 'Sfi.Site')}
		@process.json = ${Json.stringify(value)}
		@process.wrapWithScriptTag = ${'<script>window.Psmb = window.Psmb || {}; window.Psmb.i18n = window.Psmb.i18n || {}; window.Psmb.i18n = Object.assign(window.Psmb.i18n, ' + value + ');</script>'}
	}
	type = 'ourNews'
	@context.query = Sfi.Site:StreamQuery
	filterBar = T:Case {
		news {
			condition = ${q(node).property('_nodeType.name') == 'Sfi.Site:News'}
			renderer =  T:Collection {
				collection = ${q(node).property('tags') || [site]}
				itemName = 'node'
				itemRenderer = T:Tag {
					attributes.data-url = NodeUri {
						node = ${node}
					}
					attributes.class = 'filter-bar__item js-filter-bar__item'
					# akward way to fallback to all news
					content = ${q(node).property('tags') ? node.properties.title : Translation.translate('allArticles', null, [], null, 'Sfi.Site')}
				}
			}
		}
		default {
			condition = ${true}
			renderer = T:Array {
				@context.aggregations = Sfi.Site:StreamAggregations
				all = T:Tag {
					attributes.class = 'filter-bar__item js-filter-bar__item'
					attributes.data-url = NodeUri {
						node = ${node}
					}
					content = ${Translation.translate('allArticles', null, [], null, 'Sfi.Site')}
					# content.@process.count = ${value + ' (' + query.count() + ')'}
				}
				noType = T:Tag {
					attributes.class = 'filter-bar__item js-filter-bar__item'
					attributes.data-url = NodeUri {
						node = ${node}
						additionalParams {
							media = 'noType'
						}
					}
					content = 'без типа'
					@if.onlyInBackend = ${node.context.inBackend}
				}
				media = T:Collection {
					collection = ${aggregations.media.buckets}
					itemName = 'bucket'
					itemKey = 'bucketName'
					itemRenderer = T:Tag {
						@if.notEmpty = ${bucket.doc_count > 0}
						attributes.data-url = NodeUri {
							node = ${node}
							additionalParams {
								media = ${bucketName}
							}
						}
						attributes.class = 'filter-bar__item js-filter-bar__item'
						content = T:Case {
							isPhoto {
								condition = ${bucketName == 'isPhoto'}
								renderer = ${Translation.translate('photo', null, [], null, 'Sfi.Site')}
							}
							isVideo {
								condition = ${bucketName == 'isVideo'}
								renderer = ${Translation.translate('video', null, [], null, 'Sfi.Site')}
							}
							isAudio {
								condition = ${bucketName == 'isAudio'}
								renderer = ${Translation.translate('audio', null, [], null, 'Sfi.Site')}
							}
							# @process.count = ${value + ' (' + bucket.doc_count + ')'}
						}
					}
				}
				places = T:Collection {
					collection = ${aggregations.places.buckets}
					itemName = 'bucket'
					itemRenderer = T:Tag {
						@context.node = ${q(site).find('#' + bucket.key).get(0)}
						attributes.data-url = NodeUri {
							node = ${node}
							additionalParams {
								place = ${bucket.key}
							}
						}
						attributes.data-filter-place = ${node.identifier}
						attributes.data-place-title = ${node.properties.title}
						attributes.data-place-coordinates = ${node.properties.coordinates}
						attributes.class = 'filter-bar__item js-filter-bar__item js-place'
						content = ${node.properties.title}
						# content.@process.count = ${value + ' (' + bucket.doc_count + ')'}
					}
					# Set from Exhibitions
					@if.notMain = ${showPlaces ? true : false}
				}
			}
		}
	}
}

prototype(Sfi.Site:StreamAjax) < prototype(T:Value) {
	@context.itemsPerPage = 12
	@context.currentPage = ${request.arguments.currentPage ? String.toInteger(request.arguments.currentPage) : 1}
	# Used in NewsShort
	@context.isStream = ${true}
	@context.query = Sfi.Site:StreamQuery
	value = T:RawArray {
		loadMore = ${query.count() > currentPage * itemsPerPage ? true : false}
		content = T:Collection {
			collection = ${query.sortDesc('date').from((currentPage - 1) * itemsPerPage).limit(itemsPerPage).execute()}
			itemRenderer = Sfi.Site:NewsShort
		}
		@process.json = ${Json.stringify(value)}
	}
	@cache {
		mode = 'cached'
		entryTags {
			1 = 'NodeType_Sfi.Site:News'
		}
		entryIdentifier {
			node = ${node}
			pagination = ${request.arguments.currentPage}
			ajax = ${request.arguments.ajax}
			collection = ${request.arguments.collection}
			place = ${request.arguments.place}
			media = ${request.arguments.media}
			type = ${request.arguments.type}
		}
	}
}


root.stream {
	@position = 'before thePage'
	condition = ${request.arguments.ajax == 'true'}
	renderer = Sfi.Site:StreamAjax
}

root.@cache.entryIdentifier {
	node = ${node}
	pagination = ${request.arguments.currentPage}
	ajax = ${request.arguments.ajax}
	collection = ${request.arguments.collection}
	place = ${request.arguments.place}
	media = ${request.arguments.media}
	type = ${request.arguments.type}
}
