prototype(Sfi.Site:BlogList) < prototype(T:Collection) {
	@context.limit = ${limit ? limit : 10000}
	@context.node = ${q(site).children('blogs').get(0)}
	query = ${Search.query(site).nodeType('Sfi.Site:Blog').sortDesc('__sortIndex')}
	query.@process.filterOutSelf = ${value.queryFilter('bool', {must_not: [
		{term: {__identifier: documentNode.identifier}}
	]})}
	query.@process.limit = ${limit ? value.limit(limit) : value}
	@if.results = ${this.query.count() > 0 ? true : false}
	collection = ${this.query.execute()}
	itemRenderer = Sfi.Site:BlogShort

	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${documentNode}
		}
		entryTags {
			1 = 'NodeType_Sfi.Site:Blog'
		}
	}
}

prototype(Sfi.Site:BlogListWithLinks) < prototype(T:Array) {
	@process.tmpl = ${'<div class="wrap blogs"><a name="blogs"></a>' + value + '</div>'}
	@context.limit = 6
	title = ${'<h2 data-magellan-destination="blogs" class="block-title-divider"><span>' + q(site).children('blogs').property('title') + '</span></h2>'}
	list = Sfi.Site:BlogList
	link = T:Tag {
		@process.tmpl = ${'<div>' + value + '</div>'}
		tagName = 'a'
		attributes.class = 'action--small primary-color'
		attributes.href = NodeUri {
			node = ${q(site).children('blogs').get(0)}
		}
		content = ${Translation.translate('more', null, [], null, 'Sfi.Site')}
	}
}
