prototype(Sfi.Site:FestivalLayout) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Sfi.Site/Private/Fusion/Objects/FestivalLayout.html'

	festivalsMenu = Sfi.Site:FestivalsMenu


	navSectionMenu = Sfi.Site:NavSectionMenu {
		evening = Neos.Fusion:Tag {
			tagName = 'a'
			attributes.class = 'NavSectionMenu-item light-button color-white section-alt-color-bg button--alt marginHorizontal-half marginVertical-single'
			attributes.href = NodeUri {
				node = ${festivalNode}
				@process.hash = ${value + '#evening'}
			}
			content = ${Array.first(node.context.dimensions.language) == 'en' ? 'Evening Programme' : 'Вечерняя программа'}
		}
		langLink = Neos.Fusion:Tag {
			@context.targetLang = ${Array.first(node.context.dimensions.language) == 'ru' ? 'en' : 'ru'}
			tagName = 'a'
			attributes.style = 'background-color: white; color: #1e5a86 !important;'
			attributes.class = 'NavSectionMenu-item light-button marginHorizontal-half marginVertical-single'
			attributes.title = ${targetLang == 'en' ? 'English Version' : 'Русская версия сайта'}
			attributes.href = NodeUri {
				node = ${q(originalDocumentNode).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).count() > 0 ?
				q(originalDocumentNode).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).get(0) :
				q(festivalNode).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).get(0)}
			}
			content = ${targetLang == 'en' ? 'English' : 'Русский'}
		}
		@context.originalDocumentNode = ${documentNode}
		@context.documentNode = ${festivalNode}
		@process.program {
			@if.2019 = ${festivalNode.properties.uriPathSegment == '2019'}
			expression = ${this.evening + value}
			@position = 'after addLangLink'
		}
		@process.addLangLink {
			expression = ${value + this.langLink}
			@position = 'before tmpl'
		}
	}

	lang = ${Array.first(node.context.dimensions.language)}
	headline = ${q(festivalNode).property('headline')}
	subHeadline = ${q(festivalNode).property('subHeadline')}
	location = ${q(festivalNode).property('location')}
	summary = ${q(festivalNode).property('summary')}
	coverImageUri = ImageUri {
		asset = ${q(festivalNode).property('coverImage')}
	}
	linkToFestival = NodeUri {
		node = ${festivalNode}
	}

	isMain = ${festivalNode == documentNode ? true : false}

	value = ${value}
	@process.contentElementWrapping = ContentElementWrapping {
		@context.node = ${festivalNode}
	}

	year = ${Date.format(Date.now(), 'Y')}
}
