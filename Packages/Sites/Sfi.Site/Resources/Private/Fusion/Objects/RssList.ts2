prototype(Sfi.Site:RssList) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Sfi.Site/Private/Fusion/Objects/RssList.html'
	@context.collection = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('isYandex', true).sortDesc('date').greaterThan('date', Date.format(Date.subtract(Date.now(), 'P7D'), "Y-m-d\TH:i:sP")).execute()}

	childItems = Neos.Fusion:Collection {
		collection = ${collection}
		itemRenderer = Sfi.Site:RssItem
		itemName = 'node'
	}
	@cache {
		mode = 'cached'
		# Elements should be removed, once their age reaches 1W. Find out the time last element in collection has left to live
		maximumLifetime = ${q(collection).last().property('date').timestamp - Date.subtract(Date.now(), 'P7D').timestamp}
		entryTags {
			1 = ${'NodeType_Sfi.Site:News'}
		}
	}
}

prototype(Sfi.Site:RssItem) < prototype(Neos.Fusion:Template) {
	templatePath = 'resource://Sfi.Site/Private/Fusion/Objects/RssItem.html'
	node = ${node}
	title = ${String.stripTags(q(node).property('title'))}
	author = ${String.stripTags(q(node).property('author'))}
	date = ${q(node).property('date')}
	teaser = ${String.stripTags(q(node).property('teaser'))}
	@context.main = Neos.Neos:ContentCollection {
		nodePath = 'main'
	}
	bodytext = ${'<yandex:full-text>' + String.Trim(String.htmlSpecialChars(String.stripTags(String.pregReplace(String.pregReplace(main, '/(<\/[^>]+?>)(<[^>\/][^>]*?>)/', '$1 $2'), '/<\w[^>]+?>/', ' ')))) + '</yandex:full-text>'}
}

root.rss {
	@position = 'before shortcut'
	condition = ${documentNode.name == 'node-57ceae99a8c41' && node.context.workspaceName == 'live'}
	renderer = Sfi.Site:RssList
}
