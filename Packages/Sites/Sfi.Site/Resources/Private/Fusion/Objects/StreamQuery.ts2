prototype(Sfi.Site:StreamQuery) < prototype(T:Case) {
	announcements {
		condition = ${node.name == 'node-57c7aa010c560'}
		renderer = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('type', 'announcement').sortDesc('date')}
	}
	externalNews {
		condition = ${node.name == 'node-57cea530c2c46'}
		renderer = ${Search.query(site).nodeType('Sfi.Site:News').exactMatch('type', 'externalNews').sortDesc('date')}
	}
	ourNews {
		condition = ${true}
		renderer = T:Value {
			value = ${Search.query(site).nodeType('Sfi.Site:News')}
			value.@process.filterOutExternalAndAnnouncementsAndSelf = ${value.queryFilter('bool', {must_not: [
				{term: {__identifier: documentNode.identifier}}
			]})}
			value.@process.newsPages = T:Case {
				notNews {
					condition = ${q(node).is('[instanceof Sfi.Site:News]') ? false : true}
					renderer = ${value}
				}
				terms {
					condition = ${node.properties.tags || node.properties.places || node.properties.projects || node.properties.collections}
					renderer = ${value.queryFilter('bool',{should: [
							{terms: {tags: node.properties.tags ? q(node.properties.tags).map('value.identifier') : []}},
							{terms: {places: node.properties.places ? q(node.properties.places).map('value.identifier') : []}},
							{terms: {projects: node.properties.projects ? q(node.properties.projects).map('value.identifier') : []}},
							{terms: {collections: node.properties.collections ? q(node.properties.collections).map('value.identifier') : []}}
						]
					})}
				}
				sections {
					condition = ${node.properties.sections}
					renderer = ${value.queryFilter('bool',
					{should: [
							{terms: {sections: q(node.properties.sections).map('value.identifier')}}
						], must_not: [
							{term: {type: 'externalNews'}},
							{term: {type: 'announcement'}}
						]
					})}
				}
				default {
					condition = ${true}
					renderer = ${value.exactMatch('type', 'ourNews')}
				}
			}
			value.@process.tagsPages = ${q(node).is('[instanceof Sfi.Site:TagMixin]') ? value.queryFilter('bool',
			{should: [
					{term: {sections: node.identifier}},
					{term: {tags: node.identifier}},
					{term: {places: node.identifier}},
					{term: {projects: node.identifier}},
					{term: {collections: node.identifier}}
				], must_not: [
					{term: {type: 'announcement'}}
				]
			}) : value}
			value.@process.postFilters = T:Case {
				skipAggregation {
					condition = ${isAggregation}
					renderer = ${site == node ? value.exactMatch('type', 'ourNews') : value}
				}
				place {
					condition = ${request.arguments.place}
					renderer = ${value.exactMatch('places', request.arguments.place)}
				}
				collection {
					condition = ${request.arguments.collection}
					renderer = ${value.exactMatch('collections', request.arguments.collection)}
				}
				photo {
					condition = ${request.arguments.media == 'isPhoto'}
					renderer = ${value.exactMatch('isPhoto', true)}
				}
				audio {
					condition = ${request.arguments.media == 'isAudio'}
					renderer = ${value.exactMatch('isAudio', true)}
				}
				video {
					condition = ${request.arguments.media == 'isVideo'}
					renderer = ${value.exactMatch('isVideo', true)}
				}
				noType {
					condition = ${request.arguments.media == 'noType'}
					renderer = ${value.queryFilter('bool', {must_not: [
						{term: {type: 'ourNews'}},
						{term: {type: 'externalNews'}},
						{term: {type: 'announcement'}}
					]})}
				}
				default {
					condition = ${true}
					renderer = ${site == node ? value.exactMatch('type', 'ourNews') : value}
				}
			}
			value.@process.dateFilters = T:Case {
				dateFilter {
					condition = ${request.arguments.dateTill}
					dateLower = ${Date.parse(request.arguments.dateTill, "Y-m-d")}
					dateHigher = ${Date.add(this.dateLower, "P1M")}
					renderer = ${value.greaterThan('date', Date.format(this.dateLower, "Y-m-d\TH:i:sP")).lessThan('date', Date.format(this.dateHigher, "Y-m-d\TH:i:sP"))}
				}
				default {
					condition = ${true}
					renderer = ${value}
				}
			}
		}
	}
}
