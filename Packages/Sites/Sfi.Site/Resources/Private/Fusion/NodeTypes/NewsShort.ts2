prototype(Sfi.Site:NewsShort) < prototype(T:Template) {
	templatePath = 'resource://Sfi.Site/Private/Fusion/NodeTypes/NewsShort.html'
	sectionName = ${node.properties.isImportant ? 'important' : 'normal'}
	attributes = T:Attributes {
		class = T:RawArray {
			base = 'media js-media'
			important = ${node.properties.isImportant && 'media-important'}
			stream = ${isStream && 'stream__item js-stream__item'}
			photo = ${node.properties.isPhoto && 'photo'}
			video = ${node.properties.isVideo && 'video'}
			audio = ${node.properties.isAudio && 'audio'}
		}
	}
	title = ${String.cropAtWord(String.stripTags(node.properties.title), 90, '...')}
	teaser = ${String.cropAtWord(String.stripTags(node.properties.teaser), node.properties.isImportant ? 180 : 120, '...')}
	date = ${node.properties.date}
	nodeUri = NodeUri {
		node = ${node}
	}
	thumbUri = ImageUri {
		attributes.class = 'media-small__image media-small--left__image'
		asset = Sfi.Site:NewsThumb
		# No idea why this is needed
		maximumWidth>
		width = ${node.properties.isImportant ? 576 : 276}
		height = ${node.properties.isImportant ? 357 : 200}
		allowCropping = TRUE
	}
	@cache {
		mode = 'cached'
		entryIdentifier {
			node = ${node}
		}
		entryTags {
			1 = ${'Node_' + node.identifier}
			2 = ${'DescendantOf_' + q(node).children('cover').get(0).identifier}
		}
	}
}
