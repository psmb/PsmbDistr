prototype(Sfi.Site:PresentationPage) {
	templatePath = ${'resource://Sfi.Site/Private/Fusion/NodeTypes/PresentationPage-' + lang + '.html'}

	faces = T:Collection {
		@process.tmpl = ${'<div id="ri-grid" class="ri-grid ri-grid-size-2"><ul>' + value + '</ul></div>'}
		itemName = 'asset'
		collection = ${MediaQuery.imagesByTag('PresentationFaces')}
		itemRenderer = ImageTag {
			@process.tmpl = ${'<li><a href="/o-nas.html/#">' + value + '</a></li>'}
			asset = ${asset}
			width = 150
			height = 150
			allowCropping = ${true}
			@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
		}
	}

	sections = T:Collection {
		@process.tmpl = ${'<ul class="paddingTop-single">' + value + '</ul>'}
		collection = ${q(site).children('sections').children('[_hiddenInIndex != true]').get()}
		itemRenderer = T:Tag {
			tagName = 'a'
			attributes.href = NodeUri {
				node = ${node}
			}
			content = ${'<li class="paragraph primary-color">' + node.properties.title + '</li>'}
		}
	}

	ourHistoryUri = NodeUri {
		node = ${q(site).children('a').children('svyashchennik-georgiy-kochetkov-nashe-dvizhenie-vozniklo-estestvenno').get(0)}
	}

	principlesUri = NodeUri {
		node = ${q(site).find('#10c8b043-7348-4e94-866d-a49c0a6e2281').get(0)}
	}

	ourTeachers = T:Collection {
		@process.tmpl = ${'<ul class="teachers [ large-block-grid-5 medium-block-grid-5 small-block-grid-2 ]">' + value + '</ul>'}
		collection = ${node.properties.teachers}
		itemRenderer = T:Tag {
			@process.tmpl = ${'<li>' + value + '</li>'}
			tagName = 'a'
			attributes.href = NodeUri {
				node = ${node}
			}
			content = T:Array {
				image = ImageTag {
					@process.tmpl = ${'<div class="teachers__imgwrap">' + value + '</div>'}
					asset = ${node.properties.image}
					width = 242
					height = 242
					allowCropping = ${true}
				}
				caption = ${'<span class="teachers__caption">' + node.properties.title + '</span>'}
			}
		}
	}
}
