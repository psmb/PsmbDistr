root.sermonsFilteredJson {
  condition = ${request.arguments.listSermons}
  renderer = Neos.Fusion:Http.Message {
    httpResponseHead {
      headers.Content-Type = 'application/json;charset=utf-8'
      headers.Access-Control-Allow-Origin = '*'
    }
    content = Sfi.Site:SermonsFilteredJson
  }
}
root.@cache.entryIdentifier.author = ${request.arguments.author}
root.@cache.entryIdentifier.theme = ${request.arguments.theme}
root.@cache.entryIdentifier.listSermons = ${request.arguments.listSermons}

prototype(Sfi.Site:SermonsFilteredJson) < prototype(Neos.Fusion:RawCollection) {
  collection = ${Search.query(node).nodeType("Sfi.Site:Sermon")}
  collection.@process.filterByAuthor = ${request.arguments.author ? value.exactMatch('author', request.arguments.author) : value}
  collection.@process.filterByTheme = ${request.arguments.theme ? value.exactMatch('themes', request.arguments.theme) : value}
  collection.@process.exec = ${value.from(0).limit(1000).execute().toArray()}
  itemName = 'node'
  itemRenderer = Sfi.Site:SermonJson

  @process.1 = ${Json.stringify(value)}

  @cache {
    mode = 'cached'
    entryIdentifier {
      author = ${request.arguments.author}
      theme = ${request.arguments.theme}
    }
    entryTags {
      1 = ${Neos.Caching.nodeTypeTag('Sfi.Site:Sermon')}
    }
  }
}